---
rules:
    - ../../rules.hrf
    - ../../../../Cardinal Care Core/rules.hrf
---


infectious_disease_for_which_cdc_acip_recommends_immunization(influenza_disease)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Limitations from Schedule of Benefits
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% Referenced in the superfolder benefit rules file

%% Influenza vaccine
% No vaccine coverage for children less than 6 months old
meets_or_exceeds_2024_cdc_acip_age_guideline_max_by_age(0, Claim) :-
    ~patient_geq_6_months_old_at_time_of_service(Claim)

% For ages >= 9 years, one dose annually (unless received first dose when 8, and needed two doses at the time.)
meets_or_exceeds_2024_cdc_acip_age_guideline_max_by_age(Age, Claim) :-
    leq(9, Age) &
    claim.2024_2025_flu_vaccine_history(Claim, Vaccines) &
    max_doses_flu_vaccine_allowed_9_years_and_up(Claim, MaxDoses) &
    evaluate(length(Vaccines), ReceivedDoses) &
    leq(MaxDoses, ReceivedDoses)

% If child aged 9 had received precisely one dose when they were 8 and they were allowed to receive two doses, then another is allowed.
max_doses_flu_vaccine_allowed_9_years_and_up(Claim, 2) :-
    claim.2024_2025_flu_vaccine_history(Claim, [influenza_vaccination_event(VaccinationDate)]) &
    claim.patient_dob(Claim, PatientDOB) &
    patient_age_on_date(PatientDOB, VaccinationDate, 8) &
    max_doses_flu_vaccine_allowed_for_child_6_months_to_9_years(Claim, 2)

% Otherwise, they can only receive one dose
max_doses_flu_vaccine_allowed_9_years_and_up(Claim, 1) :-
    ~max_doses_flu_vaccine_allowed_9_years_and_up(Claim, 2)
    
% For ages [6 months, 9 years), depends on number of doses received in prior seasons
meets_or_exceeds_2024_cdc_acip_age_guideline_max_by_age(Age, Claim) :-
    patient_geq_6_months_old_at_time_of_service(Claim) &
    less(Age, 9) &
    max_doses_flu_vaccine_allowed_for_child_6_months_to_9_years(Claim, MaxDoses) &
    claim.2024_2025_flu_vaccine_history(Claim, Vaccines) &
    evaluate(length(Vaccines), ReceivedDoses) &
    leq(MaxDoses, ReceivedDoses)

    % 1 dose if child had previously received >= 2 doses of tri- or quadrivalent flu vaccine >= 4 weeks apart before July 2024
    % 2 doses >= 4 weeks apart if not, or if child's vaccination history is unknown
max_doses_flu_vaccine_allowed_for_child_6_months_to_9_years(Claim, 1) :-
    claim.patient_had_received_at_least_two_doses_flu_vaccine_before_july_2024(Claim, yes)

% Intentional negation as failure, as this holds both when claim.patient_had_received_at_least_two_doses_flu_vaccine_before_july_2024 is unknown and when it is no
max_doses_flu_vaccine_allowed_for_child_6_months_to_9_years(Claim, 2) :-
    ~claim.patient_had_received_at_least_two_doses_flu_vaccine_before_july_2024(Claim, yes)
