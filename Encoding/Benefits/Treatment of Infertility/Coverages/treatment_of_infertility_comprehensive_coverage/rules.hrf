---
rules:
    - ../../rules.hrf
    - ../../patient_is_infertile Definition/rules.hrf
    - ../../../../Cardinal Care Core/Precertification Required/rules.hrf
    - ../../Non-basic Infertility Services/rules.hrf
---


%% Comprehensive infertility services
covered_under_cardinal_care_24_25_benefit(C, treatment_of_infertility_benefit) :-
    claim.service_type(C, ServiceType) & 
    is_comprehensive_infertility_service(ServiceType) &
    patient_is_infertile(C) &
    meets_comprehensive_infertility_service_eligibility_requirements(C)

meets_comprehensive_infertility_service_eligibility_requirements(C) :-
    claim.patient_has_had_voluntary_sterilization(C, no) &
    claim.demonstrated_and_documented_condition_causing_infertility(C, Condition) &
    ~member(Condition, [
        natural_menopause,
        natural_perimenopause,
        natural_male_reproductive_aging,
        other_natural_physiologic_process
    ]) &
    claim.successful_pregnancy_can_be_attained_through_less_costly_treatment_covered_by_plan(C, no) &
    meets_policy_wording_table_reqs(C) % This encodes the last two criteria bullets in the "Infertility Services" heading on page 62

%%%% From 2023-2024 Certificate of Coverage table on pages 62 and 63 %%%%%
%% Female under 35 with a male partner %%
meets_policy_wording_table_reqs(C) :-
    claim.sex_of_patient_trying_to_conceive(C, female) &
    claim.sex_of_patients_partner(C, male) &
    patient_age_at_time_of_service(C, Age) &
    less(Age, 35) &
    meets_policy_wording_table_reqs_egg_sperm_contact_female_under_35_with_male_partner(C) &
    most_recent_day_3_fsh_test(C, fsh_test(Level, TestDate, TestTime)) &
    claim.date_service_received(C, ServiceDate) &
    leq_n_months_diff(TestDate, ServiceDate, 12) &
    less(Level, 19)

meets_policy_wording_table_reqs_egg_sperm_contact_female_under_35_with_male_partner(C) :-
    claim.num_months_timed_unprotected_intercourse(C, N) &
    leq(12, N)

meets_policy_wording_table_reqs_egg_sperm_contact_female_under_35_with_male_partner(C) :-
    claim.num_cycles_donor_insemination(C, N) &
    leq(12, N)


%% Female under 35 without a male partner %%
meets_policy_wording_table_reqs(C) :-
    claim.sex_of_patient_trying_to_conceive(C, female) &
    ~claim.sex_of_patients_partner(C, male) &
    patient_age_at_time_of_service(C, Age) &
    less(Age, 35) &
    claim.num_cycles_donor_insemination(C, N) &
    leq(12, N) &
    most_recent_day_3_fsh_test(C, fsh_test(Level, TestDate, TestTime)) &
    claim.date_service_received(C, ServiceDate) &
    leq_n_months_diff(TestDate, ServiceDate, 12) &
    less(Level, 19)

%% Female 35 or over with a male partner %%
meets_policy_wording_table_reqs(C) :-
    claim.sex_of_patient_trying_to_conceive(C, female) &
    claim.sex_of_patients_partner(C, male) &
    patient_age_at_time_of_service(C, Age) &
    leq(35, Age) &
    meets_policy_wording_table_reqs_egg_sperm_contact_female_35_or_over_with_male_partner(C) &
    most_recent_day_3_fsh_test(C, fsh_test(_, TestDate, TestTime)) &
    claim.date_service_received(C, ServiceDate) &
    leq_n_months_diff(TestDate, ServiceDate, 6) &
    meets_day_3_fsh_level_criteria_female_35_or_over(C)

meets_policy_wording_table_reqs_egg_sperm_contact_female_35_or_over_with_male_partner(C) :-
    claim.num_months_timed_unprotected_intercourse(C, N) &
    leq(6, N)

meets_policy_wording_table_reqs_egg_sperm_contact_female_35_or_over_with_male_partner(C) :-
    claim.num_cycles_donor_insemination(C, N) &
    leq(6, N)


%% Female 35 or over without a male partner %%
meets_policy_wording_table_reqs(C) :-
    claim.sex_of_patient_trying_to_conceive(C, female) &
    ~claim.sex_of_patients_partner(C, male) &
    patient_age_at_time_of_service(C, Age) &
    leq(35, Age) &
    claim.num_cycles_donor_insemination(C, N) &
    leq(6, N) &
    most_recent_day_3_fsh_test(C, fsh_test(_, TestDate, TestTime)) &
    claim.date_service_received(C, ServiceDate) &
    leq_n_months_diff(TestDate, ServiceDate, 6) &
    meets_day_3_fsh_level_criteria_female_35_or_over(C)

% Day 3 FSH level criteria for women 35 or over %
% If under 40, only care about most recent test
meets_day_3_fsh_level_criteria_female_35_or_over(C) :-
    patient_age_at_time_of_service(C, Age) &
    less(Age, 40) &
    most_recent_day_3_fsh_test(C, fsh_test(Level, _, _)) &
    less(Level, 19)

% If 40 or over, care about all tests performed since age 40 (and there must be at least one test)
meets_day_3_fsh_level_criteria_female_35_or_over(C) :-
    patient_age_at_time_of_service(C, Age) &
    leq(40, Age) &
    day_3_fsh_tests_since_age_40_in_claim(C, Tests) &
    evaluate(length(Tests), NumTests) &
    leq(1, NumTests) &
    ~exists_test_with_level_at_least_19(Tests)

%% Male with female partner under 35 %%
meets_policy_wording_table_reqs(C) :-
    claim.sex_of_patient_trying_to_conceive(C, male) &
    claim.sex_of_patients_partner(C, female) &
    claim.age_of_patients_female_partner(C, Age) &
    less(Age, 35) &
    claim.num_months_timed_unprotected_intercourse(C, N) &
    leq(12, N)

%% Male with female partner 35 or over %%
meets_policy_wording_table_reqs(C) :-
    claim.sex_of_patient_trying_to_conceive(C, male) &
    claim.sex_of_patients_partner(C, female) &
    claim.age_of_patients_female_partner(C, Age) &
    leq(35, Age) &
    claim.num_months_timed_unprotected_intercourse(C, N) &
    leq(6, N)


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Medical necessity
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Source: A.3 from CPB 0327 on 1/4/2025: https://www.aetna.com/cpb/medical/data/300_399/0327.html
medically_necessary_service(C) :-
    claim.service_type(C, intrauterine_insemination)

medically_necessary_service(C) :-
    claim.service_type(C, ovulation_induction_cycle_with_menotropins) &
    patient_age_at_time_of_service(C, Age) &
    leq(Age, 37) &
    meets_cpb_egg_sperm_contact_trial_reqs(C)


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Precertified if necessary
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

requires_precertification(ServiceType) :-
    is_comprehensive_infertility_service(ServiceType)

is_comprehensive_infertility_service(ovulation_induction_cycle_with_menotropins)
is_comprehensive_infertility_service(intrauterine_insemination)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Limitations from Schedule of Benefits
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Note: later encodings will compute these from other claims, but right now we're only doing one claim at a time
% Maximum six covered attempts at IUI in a policy year (between September 1 2024 and August 31 2025, in this case)
beyond_limits(Claim) :-
    claim.service_type(Claim, intrauterine_insemination) &
    claim.num_other_covered_artificial_insemination_attempts(Claim, N) &
    leq(6, N)
