---
rules:
    - ../../rules.hrf
    - ../../Non-basic Infertility Services/rules.hrf
---


%% Advanced reproductive technology services
covered_under_cardinal_care_24_25_benefit(C, treatment_of_infertility_benefit) :-
    claim.service_type(C, ServiceType) & 
    is_art_service(ServiceType) &
    meets_fertility_preservation_eligibility_requirements(C) &
    ~claim.location_service_received(C, other) % ART service-specific exclusion in the infertility treatment benefit. Not provided for out-of-network care.


meets_fertility_preservation_eligibility_requirements(C) :-
    claim.patient_is_believed_to_be_fertile(C, yes) &
    claim.planned_service(C, PlannedService) &
    member(PlannedService, [
        chemotherapy,
        pelvic_radiotherapy,
        gonadotoxic_therapy,
        orchiectomy,
        oophorectomy,
        hysterectomy,
        orchiectomy_for_treatment_of_disease,
        oophorectomy_for_treatment_of_disease,
        other_treatment_demonstrated_to_result_in_infertility
    ]) &
    patient_age_at_time_of_service(C, Age) &
    eggs_to_be_retrieved_meet_fsh_and_ovarian_responsiveness_criteria(C, Age)

% Vacuously satisfied if not retrieving eggs for use
eggs_to_be_retrieved_meet_fsh_and_ovarian_responsiveness_criteria(C, _) :-
    claim.will_be_retrieving_eggs_for_use(C, no)

% Source: Note 4 from CPB 0327 on 1/4/2025: https://www.aetna.com/cpb/medical/data/300_399/0327.html
% If under 40, just care about most recent test
eggs_to_be_retrieved_meet_fsh_and_ovarian_responsiveness_criteria(C, Age) :-
    claim.will_be_retrieving_eggs_for_use(C, yes) &
    less(Age, 40) &
    most_recent_day_3_fsh_test(C, fsh_test(Level, Date, Time)) &
    less(Level, 19) &
    meets_fsh_test_recency_req(C, fsh_test(Level, Date, Time))

% If 40 or over, care about all prior tests since became 40 (This is inferred from the "Comprehensive infertility services" FSH level reqs, the Note 4 text "Check benefit plan descriptions", and the Note 5 text "even if she has had an elevated D3 FSH level above 40 years of age in the past")
% But needs at least one within the last 6 months
% Another practical assumption: There must be at least one test after age 40
eggs_to_be_retrieved_meet_fsh_and_ovarian_responsiveness_criteria(C, Age) :-
    claim.will_be_retrieving_eggs_for_use(C, yes) &
    leq(40, Age) &
    day_3_fsh_tests_after_age_40_in_claim(C, Tests) &
    evaluate(length(Tests), NumTests) &
    leq(1, NumTests) &
    ~exists_test_with_level_at_least_19(Tests) &
    most_recent_day_3_fsh_test(C, fsh_test(Level, Date, Time)) &
    meets_fsh_test_recency_req(C, fsh_test(Level, Date, Time))

% Source: Note 5 from CPB 0327 on 1/4/2025: https://www.aetna.com/cpb/medical/data/300_399/0327.html
% Additional (more generous) eligibility requirements if a woman has premature ovarian failure
eggs_to_be_retrieved_meet_fsh_and_ovarian_responsiveness_criteria(C, Age) :-
    claim.will_be_retrieving_eggs_for_use(C, yes) &
    leq(40, Age) &
    claim.patient_has_premature_ovarian_failure(C, yes) &
    most_recent_day_3_fsh_test(C, fsh_test(Level, Date, Time)) &
    less(Level, 19) &
    meets_fsh_test_recency_req(C, fsh_test(Level, Date, Time))


% If 39 or over, test must be in the last six months
meets_fsh_test_recency_req(C, fsh_test(_, TestDate, TestTime)) :-
    patient_age_at_time_of_service(C, Age) &
    leq(39, Age) &
    claim.date_service_received(C, ServiceDate) &
    get_timestamp_from_date(ServiceDate, ServiceDateTS) &
    evaluate(times(365, 24, 60, 60, 1000), NUM_MS_IN_SIX_MONTHS) &
    evaluate(minus(ServiceDateTS, NUM_MS_IN_SIX_MONTHS), SixMonthsBeforeTS) &
    datetimetotimestamp(TestDate, TestTime, TestDateTimeTS) &
    leq(SixMonthsBeforeTS, TestDateTimeTS)

% If under 39, no recency requirement
meets_fsh_test_recency_req(C, _) :-
    patient_age_at_time_of_service(C, Age) &
    less(Age, 39)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Medical necessity
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% Cryopreservation for fertility preservation %%%

% Almost redundant with list of valid services listed in the policy itself
% Is also more restrictive than the planned service constraint in the policy's eligibility reqs, even though the policy reqs are likely to be the ones honored here
% Doesn't have the requirements about appropriate trials of egg-sperm contact, because this is treatment for expected imminent infertility, rather than current infertility
medically_necessary_service(C) :-
    claim.service_type(C, cryopreservation_for_fertility_preservation) &
    claim.planned_service(C, PlannedService) &
    member(PlannedService, [
        chemotherapy,
        pelvic_radiotherapy,
        gonadotoxic_therapy,
        orchiectomy_for_treatment_of_disease,
        oophorectomy_for_treatment_of_disease
    ]) &
    younger_than_40_or_has_provided_evidence_not_menopausal(C)

% Source: Note 5 from CPB 0327 on 1/4/2025: https://www.aetna.com/cpb/medical/data/300_399/0327.html
% Applicable to all ART services
younger_than_40_or_has_provided_evidence_not_menopausal(C) :-
    patient_age_at_time_of_service(C, Age) &
    less(Age, 40)

younger_than_40_or_has_provided_evidence_not_menopausal(C) :-
    patient_age_at_time_of_service(C, Age) &
    leq(40, Age) &
    day_3_fsh_tests_after_age_40_in_claim(C, Tests) &
    evaluate(length(Tests), NumTests) &
    leq(1, NumTests) &
    ~exists_test_with_level_at_least_19(Tests)

%%% IVF for fertility preservation %%%

medically_necessary_service(C) :-
    claim.service_type(C, ivf_for_fertility_preservation) & 
    meets_ivf_cpb_reqs(C)

% Source: Note 5 from CPB 0327 on 1/4/2025: https://www.aetna.com/cpb/medical/data/300_399/0327.html
% All patients under 45 with premature ovarian failure meet medical necessity reqs for IVF
meets_ivf_cpb_reqs(C) :-
    claim.patient_has_premature_ovarian_failure(C, yes) &
    patient_age_at_time_of_service(C, Age) &
    less(Age, 45)

% Source: E.3.b and Notes 2 and 5 from CPB 0327 on 1/4/2025: https://www.aetna.com/cpb/medical/data/300_399/0327.html
% Otherwise, must have undergone an appropriate trial of less invasive prior treatment, and provide evidence that they are not menopausal if they are 40 or older
meets_ivf_cpb_reqs(C) :-
    meets_less_invasive_prior_treatment_reqs(C) &
    younger_than_40_or_has_provided_evidence_not_menopausal(C)

% Source: E.3.b and Note 2 from CPB 0327 on 1/4/2025: https://www.aetna.com/cpb/medical/data/300_399/0327.html
% Rounds of less invasive prior treatment are not necessary if only IVF expected to be effective
% Note: They list some examples of such individuals, but don't seem to be comprehensive
meets_less_invasive_prior_treatment_reqs(C) :-
    claim.inseminations_not_expected_to_be_effective_and_ivf_expected_to_be_only_effective_treatment(C, yes)

meets_less_invasive_prior_treatment_reqs(C) :-
    meets_egg_sperm_contact_trial_reqs(C) &
    meets_ovulation_induction_trial_reqs(C)

% For ages under 35
meets_egg_sperm_contact_trial_reqs(C) :-
    patient_age_at_time_of_service(C, Age) &
    less(Age, 35) &
    meets_egg_sperm_contact_trial_reqs_under_35(C)

meets_egg_sperm_contact_trial_reqs_under_35(C) :-
    claim.num_months_regular_intravaginal_inseminations(C, N) &
    leq(12, N)

meets_egg_sperm_contact_trial_reqs_under_35(C) :-
    claim.num_cycles_timed_intrauterine_intracervical_inseminations(C, N) &
    leq(4, N)

% For ages 35 and over
meets_egg_sperm_contact_trial_reqs(C) :-
    patient_age_at_time_of_service(C, Age) &
    leq(35, Age) &
    meets_egg_sperm_contact_trial_reqs_35_and_over(C)

meets_egg_sperm_contact_trial_reqs_35_and_over(C) :-
    claim.num_months_regular_intravaginal_inseminations(C, N) &
    leq(6, N)

meets_egg_sperm_contact_trial_reqs_35_and_over(C) :-
    claim.num_cycles_timed_intrauterine_intracervical_inseminations(C, N) &
    leq(3, N)

% Three cycles if under 38, none necessary if 38 or over
meets_ovulation_induction_trial_reqs(C) :-
    patient_age_at_time_of_service(C, Age) &
    less(Age, 38) &
    claim.num_cycles_oral_or_injectable_ovulation_induction(C, N) &
    leq(3, N)

meets_ovulation_induction_trial_reqs(C) :-
    patient_age_at_time_of_service(C, Age) &
    leq(38, Age)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Precertified if necessary
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

requires_precertification(ServiceType) :-
    is_art_service(ServiceType)

is_art_service(ivf_for_fertility_preservation)
is_art_service(cryopreservation_for_fertility_preservation)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Limitations from Schedule of Benefits
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

beyond_limits(Claim) :-
    claim.service_type(Claim, ServiceType) &
    is_art_service(ServiceType) &
    claim.num_other_covered_art_services(Claim, N) &
    leq(1, N)
